;
; *****
; ***** TTY STATUS, INPUT AND OUTPUT DRIVERS
; ***** TOM DE LELLIS
; ***** 11/29/85
; *****
;
	.Z80
	CSEG
;
	PUBLIC	TTYIST
	PUBLIC	TTYOST
	PUBLIC	TTYOUT
	PUBLIC	TTYIN
;
SIO1DAT	EQU	X'C0'			; SIO1 DATA I/O PORT
SIO1STA	EQU	X'C1'			; SIO1 STATUS PORT
;
TXRDY	EQU	X'01'			; XMTR BUFFER EMPTY BIT
RXRDY	EQU	X'02'			; RCVR BUFFER FULL BIT
DCD	EQU	X'40'			; CARRIER DETECT
;
TRUE	EQU	X'FF'
FALSE	EQU	X'00'
;
; *****
; ***** GET INPUT STATUS
; *****
;
TTYIST:
	IN	A,(SIO1STA)		; GET STATUS
	AND	RXRDY			; CHECK FOR DATA
	RET	Z			; IF NOT, EXIT
	CPL				; NOT 0
	RET				; EXIT
;
; *****
; ***** GET OUTPUT STATUS
; *****
;
TTYOST:
	IN	A,(SIO1STA)		; GET STATUS
	AND	TXRDY+DCD		; MASK WHAT WE WANT
	CP	TXRDY+DCD		; IS IT WHAT WE WANT
	JR	Z,TTYOST1		; IF SO, SEE TTYOST1
	XOR	A			; LOAD NOT READY YET
	RET				; EXIT
TTYOST1:
	LD	A,TRUE			; LOAD TRUE
	RET				; EXIT
;
; *****
; ***** OUTPUT DATA
; *****
;
TTYOUT:
	IN	A,(SIO1STA)		; GET STATUS
	AND	TXRDY			; ARE WE READY TO SEND
	JR	Z,TTYOUT		; IF NOT, WAIT
	LD	A,C			; ELSE GET CHARACTER TO SEND
	OUT	(SIO1DAT),A		; SEND IT
	RET				; EXIT
;
; *****
; ***** INPUT DATA
; *****
;
TTYIN:
	IN	A,(SIO1STA)		; GET STATUS
	AND	RXRDY			; ARE WE READY TO RECEIVE
	JR	Z,TTYIN			; IF NOT, WAIT FOR DATA
	IN	A,(SIO1DAT)		; ELSE GET THE DATA
	RET				; AND EXIT
;
	END
