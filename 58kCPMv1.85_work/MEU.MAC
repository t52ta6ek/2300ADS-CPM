;
; *****
; ***** ADS CP/M MEU DISK DRIVERS
; ***** TOM DE LELLIS
; ***** 9/16/86
; *****
;
	.Z80
	CSEG
;
	PUBLIC	DPHM1
	PUBLIC	DPHM2
	PUBLIC	DPB07
;
	EXTRN	UDSECBF
	EXTRN	XTL00
	EXTRN	DIRBF
	EXTRN	FMPLIST
	EXTRN	UDUNIT
	EXTRN	UDTRACK
	EXTRN	UDSECTR
;
DCTLR1	EQU	X'98'			; CONTROL/STATUS
DDATA1	EQU	X'99'			; DATA
;
RD	EQU	NOT X'86'		; READ
WR	EQU	NOT X'83'		; WRITE
;
SECSIZ	EQU	128			; SECTOR SIZE
;
; *****
; ***** DISK READ SUBROUTINE
; *****
;
FREAD:
	LD	A,RD			; READ COMMAND
	CALL	CMD			; START READ, SET BUFFER, SIZE
	INIR				; INPUT SECTOR
FLOGIN:
	RET				; EXIT
;
; *****
; ***** DISK WRITE SUBROUTINE
; *****
;
FWRITE:
	LD	A,WR			; WRITE
	CALL	CMD			; START WRITE, SET BUFFER, SIZE
	OTIR				; OUTPUT SECTOR
	RET				; EXIT
;
; *****
; ***** SET UNIT/TRACK/SECTOR
; *****
;
CMD:
	OUT	(DCTLR1),A		; SEND COMMAND TO CONTROLLER
	LD	A,(UDUNIT)		; GET UNIT
	CPL				; INVERT
	OUT	(DDATA1),A		; OUTPUT
	LD	A,(UDTRACK)		; GET TRACK
	CPL				; INVERT
	OUT	(DDATA1),A		; OUTPUT
	LD	A,(UDSECTR)		; GET SECTOR
	CPL				; INVERT
	OUT	(DDATA1),A		; OUTPUT
	OUT	(DDATA1),A		; AUX BYTE
;
; *****
; ***** SETUP BUFFER, SIZE OF READ, ERROR STATUS
; *****
;
	LD	HL,(UDSECBF)		; DMA BUFFER ADDRESS
	LD	B,SECSIZ		; PUT SECTOR SIZE INTO B
	LD	C,DDATA1		; DATA PORT
	XOR	A			; NO ERROR
	RET				; DONE, RETURN TO CALLER
;
; *****
; ***** DISK PARAMETER HEADERS
; *****
;
	DSEG
;
; *****
; ***** DRIVE M:
; *****
;
	DEFW	FWRITE,FREAD
	DEFW	FLOGIN
DPHM1:
	DEFW	XTL00,0000H
	DEFW	0000H,0000H
	DEFW	DIRBF,DPB07
	DEFW	0000H,ALL07
;
; *****
; ***** DRIVE N:
; *****
;
	DEFW	FWRITE,FREAD
	DEFW	FLOGIN
DPHM2:
	DEFW	XTL00,0000H
	DEFW	0000H,0000H
	DEFW	DIRBF,DPB07
	DEFW	0000H,ALL08
;
; *****
; ***** DPB FOR M: AND N:
; *****
;
DPB07:
	DEFW	52			; SECTORS PER TRACK
	DEFB	4			; BLOCK SHIFT FACTOR
	DEFB	15			; BLOCK MASK
	DEFB	01			; EXTENT MASK
	DEFW	247-1			; DISK STORAGE CAPACITY
	DEFW	128-1			; DIRECTORY ENTRIES
	DEFB	X'C0'			; ALLOCATION VECTOR SIZE
	DEFB	X'00'			; ALLOCATION VECTOR SIZE DIRECTORY BLOC
	DEFW	0			; DIRECTORY CHECK SIZE
	DEFW	2			; RESERVED TRACKS AT BEGINNING OF DISK
;
; *****
; ***** DISK ALLOCATIONS
; *****
;
ALL07:
	DEFS	31
ALL08:
	DEFS	31
;
	END
